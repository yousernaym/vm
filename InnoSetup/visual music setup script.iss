; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Visual Music"
#define MyAppVersion "0.3.5"
#define MyAppPublisher "Mikesoft"
#define MyAppAssemblyName "VM"
#define MyAppExeName "VM.exe"
#define MyAppDataDir "{userappdata}\" + MyAppName
#define UserFiles "{userdocs}\" + MyAppName
#define MyAppAssocName MyAppName + "Project"
#define MyAppAssocExt ".vmp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt
#define BinFolder "..\VisualMusic\bin\Release\net6.0-windows"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3F797ABA-DFBA-4CB6-8F1F-DFBA6986E064}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} v{#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName} 
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename={#MyAppName} setup-win64
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode = x64
ArchitecturesAllowed = x64
ChangesAssociations=True
UninstallDisplayName={#MyAppName}
UninstallDisplayIcon={app}\{#MyAppExeName}
ShowTasksTreeLines=False
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; Flags: unchecked
Name: "docsFolder"; Description: "Create folder '<user-documents>\Visual Music'"
Name: "associateVmp"; Description: "Associate with .vmp files"

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

;Monogame
Source: "{#BinFolder}\MonoGame.Framework.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\SharpDX.Direct3D11.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\SharpDX.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\SharpDX.DXGI.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\Content\*"; DestDir: "{app}\Content"; Flags: ignoreversion createallsubdirs recursesubdirs

;Midilib
Source: "{#BinFolder}\midilib.dll"; DestDir: "{app}"; Flags: ignoreversion

;Remuxer
Source: "{#BinFolder}\Remuxer\*.dll"; DestDir: "{app}\Remuxer"; Flags: ignoreversion
Source: "{#BinFolder}\Remuxer\Remuxer.exe"; DestDir: "{app}\Remuxer"; Flags: ignoreversion
Source: "..\Dependencies\Remuxer\libRemuxer\roms\*"; DestDir: "{app}\Remuxer\Roms"; Flags: ignoreversion

;Cefsharp
Source: "{#BinFolder}\runtimes\win-x64\*"; DestDir: "{app}\runtimes\win-x64\"; Flags: ignoreversion recursesubdirs
Source: "{#BinFolder}\Web Data"; DestDir: "{app}\"; Flags: ignoreversion
Source: "{#BinFolder}\CefSharp.WinForms.dll"; DestDir: "{app}\"; Flags: ignoreversion
Source: "Files\cef-LICENSE.txt"; DestDir: "{app}"; Flags: ignoreversion


;Midmix + fluidsynth
Source: "{#BinFolder}\MidMix.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\libfluidsynth-3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\iconv-2.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\intl-8.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\pcre.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\glib-2.0-0.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\soundfont.sf2"; DestDir: "{app}"; Flags: ignoreversion

;Media + Ffmpeg
Source: "{#BinFolder}\Media.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\avcodec-58.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\avformat-58.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\avutil-56.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\libx264-163.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\swresample-3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\swscale-5.dll"; DestDir: "{app}"; Flags: ignoreversion

;Runtimes
Source: "Files\VC_redist.x64.exe"; DestDir: "{tmp}"; Flags: ignoreversion
Source: "Files\windowsdesktop-runtime-7.0.1-win-x64.exe"; DestDir: "{tmp}"; Flags: ignoreversion

;Misc
Source: "{#BinFolder}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\{#MyAppAssemblyName}.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\{#MyAppAssemblyName}.dll.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\{#MyAppAssemblyName}.runtimeconfig.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\{#MyAppAssemblyName}.deps.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\VisualMusic\xmplay.ini"; DestDir: "{#MyAppDataDir}\tparty\xmplay"; Flags: ignoreversion
Source: "{#BinFolder}\Microsoft.WindowsAPICodePack.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinFolder}\Microsoft.WindowsAPICodePack.Shell.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\songlenghts.md5"; DestDir: "{#MyAppDataDir}\tparty\hvsc"; Flags: ignoreversion

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\VC_redist.x64.exe"; Parameters:  "/quiet /norestart";  StatusMsg: "Installing Visual C++ Redistributable"
Filename: "{tmp}\windowsdesktop-runtime-7.0.1-win-x64.exe"; Parameters:  "/quiet";  StatusMsg: "Installing .NET 7 Windows desktop runtime"
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: postinstall skipifsilent

[Dirs]
Name: "{#MyAppDataDir}"; Flags: uninsalwaysuninstall
Name: "{#UserFiles}\Props"; Flags: uninsalwaysuninstall; Tasks: docsFolder
Name: "{#UserFiles}\Projects"; Flags: uninsalwaysuninstall; Tasks: docsFolder
Name: "{#UserFiles}\Videos"; Flags: uninsalwaysuninstall; Tasks: docsFolder

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue; Tasks: associateVmp
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey; Tasks: associateVmp
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"; Tasks: associateVmp
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Tasks: associateVmp
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: {#MyAppAssocExt}; ValueData: ""; Tasks: associateVmp

[UninstallDelete]
Type: filesandordirs; Name: "{#MyAppDataDir}"
Type: dirifempty; Name: "{#UserFiles}"
Type: dirifempty; Name: "{app}"